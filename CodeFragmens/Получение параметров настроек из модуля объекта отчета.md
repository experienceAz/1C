# Получение параметров настроек из модуля объекта отчета

```bsl
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	// 1 способ получения параметра пользовательской настройки
	ПараметрПериод = Новый ПараметрКомпоновкиДанных("Период");
	Для каждого Элемент Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ЗначениеПараметраКомпоновкиДанных") 
			И Элемент.Параметр = ПараметрПериод И Элемент.Использование Тогда
			ЗначениеПериод = Элемент.Значение;
			Прервать;			
		КонецЕсли;
	КонецЦикла;
	
	// 2 способ
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();   
	Элемент = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период")); 
	Если Элемент <> Неопределено И Элемент.Использование Тогда
		ЗначениеПериод = Элемент.Значение;	
	КонецЕсли;

	// 3 способ
	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
	Если Параметр <> Неопределено Тогда
		Элемент = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(Параметр.ИдентификаторПользовательскойНастройки);
		Если Элемент <> Неопределено И Элемент.Использование Тогда
			ЗначениеПериод = Элемент.Значение;	
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеПериод = Неопределено Тогда
		ДатаОтчета = ТекущаяДата();
	ИначеЕсли  ТипЗнч(ЗначениеПериод) = Тип("СтандартнаяДатаНачала") Тогда
		Если ЗначениеПериод.Дата = Дата(1,1,1) Тогда
			ДатаОтчета = ТекущаяДата();
		Иначе    
			ДатаОтчета = ЗначениеПериод.Дата;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
```
